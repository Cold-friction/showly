plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'com.google.devtools.ksp'
  id 'dagger.hilt.android.plugin'
}

apply from: '../versions.gradle'

android {
  kotlinOptions { jvmTarget = "21" }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
  }

  buildFeatures {
    buildConfig = true
    viewBinding true
  }

  hilt {
    enableExperimentalClasspathAggregation = true
  }

  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "com.michaldrabik.showly2"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    compileSdkVersion versions.compileSdk
    versionCode versions.versionCode
    versionName versions.versionName

    buildConfigField "String", "TMDB_API_KEY", "\"${System.getenv("TMDB_API_KEY")}\""
    buildConfigField "String", "TRAKT_CLIENT_ID", "\"${System.getenv("TRAKT_CLIENT_ID")}\""
    buildConfigField "String", "TRAKT_CLIENT_SECRET", "\"${System.getenv("TRAKT_CLIENT_SECRET")}\""
    buildConfigField "String", "OMDB_API_KEY", "\"${System.getenv("OMDB_API_KEY")}\""

    resourceConfigurations += ['en', 'ar', 'de', 'da', 'es', 'fi', 'fr', 'it', 'pl', 'pt', 'ru', 'tr', 'zh', 'uk']
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      storeFile new File(rootDir, "app/keystore.jks")
      storePassword System.getenv("KEYSTORE_PASSWORD")
      keyAlias System.getenv("KEY_ALIAS")
      keyPassword System.getenv("KEY_PASSWORD")
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debugoss'
      versionNameSuffix '-debug'
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      manifestPlaceholders = [
          appIcon     : "@mipmap/ic_launcher_gray",
          appIconRound: "@mipmap/ic_launcher_round_gray"
      ]
    }
    release {
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      manifestPlaceholders = [
          appIcon     : "@mipmap/ic_launcher",
          appIconRound: "@mipmap/ic_launcher_round"
      ]
    }
  }

  lint {
    checkReleaseBuilds false
  }

  dependenciesInfo {
    includeInApk = false
    includeInBundle = false
  }

  namespace 'com.michaldrabik.showly2'
}

dependencies {
  implementation project(':common')
  implementation project(':data-remote')
  implementation project(':data-local')
  implementation project(':repository')
  implementation project(':ui-base')
  implementation project(':ui-backup')
  implementation project(':ui-model')
  implementation project(':ui-navigation')
  implementation project(':ui-trakt-sync')
  implementation project(':ui-discover')
  implementation project(':ui-discover-movies')
  implementation project(':ui-episodes')
  implementation project(':ui-comments')
  implementation project(':ui-lists')
  implementation project(':ui-show')
  implementation project(':ui-movie')
  implementation project(':ui-gallery')
  implementation project(':ui-my-shows')
  implementation project(':ui-my-movies')
  implementation project(':ui-search')
  implementation project(':ui-statistics')
  implementation project(':ui-statistics-movies')
  implementation project(':ui-settings')
  implementation project(':ui-progress')
  implementation project(':ui-progress-movies')
  implementation project(':ui-premium')
  implementation project(':ui-widgets')

  implementation libs.hilt.android
  ksp libs.hilt.compiler
  implementation libs.hilt.work
  ksp libs.hilt.work.compiler

  testImplementation libs.bundles.testing
  androidTestImplementation libs.android.test.runner

  coreLibraryDesugaring libs.android.desugar
}
